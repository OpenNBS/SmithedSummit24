import glob

#> setup
function_tag minecraft:load {
    "values": [(~/load)]
}

append function ~/load:
    scoreboard objectives add nbs dummy


#> tick the song time
function_tag minecraft:tick {
    "values": [(~/tick)]
}

function ~/tick:
    execute if score playing nbs matches 1 run scoreboard players add songtime nbs 1
    execute store result storage nbs:main playing.tick int 1 run scoreboard players get songtime nbs
    function nbs:global/playback with storage nbs:main playing # TODO: this command works in a command block, but not here :c

function ~/playback:
    $function nbs:song/$(name)/$(tick)

function ~/change_song:
    execute store result storage nbs:main playing.index int 1 run scoreboard players get songindex nbs
    function nbs:global/change_song2 with storage nbs:main playing

function ~/change_song2:
    $data modify storage nbs:main playing set from storage nbs:main songs[$(index)]


song_count = len(glob.glob("songs/*.nbs"))

#> Song Controls
function ~/next:
    scoreboard players set songtime nbs -1
    scoreboard players add songindex nbs 1
    execute if score songindex nbs matches f"{song_count}.." run scoreboard players set songindex nbs 0
    function ~/../change_song

function ~/pause:
    scoreboard players set playing nbs 0

function ~/play:
    scoreboard players set playing nbs 1

function ~/prev:
    scoreboard players set songtime nbs -1
    scoreboard players remove songindex nbs 1
    execute if score songindex nbs matches ..-1 run scoreboard players set songindex nbs (song_count - 1)
    function ~/../change_song

function ~/stop:
    scoreboard players set playing nbs 0
    scoreboard players set songtime nbs -1

function ~/shuffle:
    scoreboard players set songtime nbs -1
    scoreboard players operation prevsong nbs = songindex nbs
    execute store result score songindex nbs run random value (0, song_count - 1)
    execute if score songindex nbs = prevsong nbs run function ~/next
    function ~/../change_song
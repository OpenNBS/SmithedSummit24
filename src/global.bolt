import glob

#> setup
merge function_tag minecraft:load {
    "values": [(~/load)]
}

append function ~/load:
    scoreboard objectives add nbs dummy
    scoreboard players set playing nbs 1
    scoreboard players set shuffle nbs 1


#> tick the song time
merge function_tag minecraft:tick {
    "values": [(~/tick)]
}

function ~/tick:
    execute if score playing nbs matches 1 run scoreboard players add songtime nbs 1
    execute store result storage nbs:main playing.tick int 1 run scoreboard players get songtime nbs
    function nbs:global/playback with storage nbs:main playing # TODO: this command works in a command block, but not here :c

function ~/playback:
    $function nbs:song/$(name)/$(tick)

function ~/change_song:
    scoreboard players set songtime nbs -1
    execute store result storage nbs:main playing.index int 1 run scoreboard players get songindex nbs
    function nbs:global/change_song2 with storage nbs:main playing

function ~/change_song2:
    $data modify storage nbs:main playing set from storage nbs:main songs[$(index)]
    $execute as @e[tag=aj.music_speaker.root] on passengers if entity @s[tag=aj.music_speaker.bone.text_display] run data modify entity @s text set value '{"text":"$(formatted_string)","color":"green"}'
    function nbs:global/title with storage nbs:main playing
    

#> Effects

function ~/title:
    $execute as @e[tag=nbs_speaker] at @s run title @a[distance=..12] actionbar {"text":"","extra":[{"text":"Now Playing: ","color":"green"},{"text":"$(formatted_string)","color":"white"}]}
    $execute as @e[tag=nbs_loudspeaker] at @s run title @a[distance=..48] actionbar {"text":"","extra":[{"text":"Now Playing: ","color":"green"},{"text":"$(formatted_string)","color":"white"}]}
    $title @a[tag=nbs_headphones] actionbar {"text":"","extra":[{"text":"Now Playing: ","color":"green"},{"text":"$(formatted_string)","color":"white"}]}
    $execute as @e[tag=aj.music_speaker.root] on passengers if entity @s[tag=aj.music_speaker.bone.text_display] run data modify entity @s text set value {"text":"","extra":[{"text":"Now Playing: ","color":"green"},{"text":"$(formatted_string)","color":"white"}]}

function ~/beat:
    execute store result storage nbs:main beat.beat int 1 run random value 1..4
    function nbs:global/beat2 with storage nbs:main beat

function ~/beat2:
    $execute as @e[tag=aj.music_speaker.root] at @s if entity @a[distance=..10] run function animated_java:music_speaker/animations/animation_speaker_beat_$(beat)/play


song_count = len(glob.glob("songs/*.nbs"))

#> Song Controls
function ~/next:
    scoreboard players add songindex nbs 1
    execute if score songindex nbs matches f"{song_count}.." run scoreboard players set songindex nbs 0
    function ~/../change_song

function ~/pause:
    scoreboard players set playing nbs 0

function ~/play:
    scoreboard players set playing nbs 1

function ~/prev:
    scoreboard players remove songindex nbs 1
    execute if score songindex nbs matches ..-1 run scoreboard players set songindex nbs (song_count - 1)
    function ~/../change_song

function ~/stop:
    scoreboard players set playing nbs 0
    scoreboard players set songtime nbs -1

function ~/shuffle:
    scoreboard players operation prevsong nbs = songindex nbs
    execute store result score songindex nbs run random value (0, song_count - 1)
    execute if score songindex nbs = prevsong nbs run function ~/../next
    function ~/../change_song

function ~/advance:
    execute if score shuffle nbs matches 0 run function ~/../next
    execute if score shuffle nbs matches 1 run function ~/../shuffle

import glob

#> Setup

merge function_tag minecraft:load {
    "values": [(~/load)]
}

append function ~/load:
    scoreboard objectives add nbs dummy
    scoreboard players set playing nbs 0
    scoreboard players set songtime nbs -1
    function ~/../summon_sign
    advancement revoke @a only nbs:lever_interaction
    schedule function ~/../tick 1t replace
    function ~/../destroy_lever

function ~/tick:
    schedule function (~/) 1t replace
    execute if score playing nbs matches 1 run scoreboard players add songtime nbs 1
    execute store result storage nbs:main playing.tick int 1 run scoreboard players get songtime nbs
    function nbs:panel/playback with storage nbs:main playing

function ~/playback:
    $function nbs:song/$(name)/$(tick)

function ~/change_song:
    scoreboard players set songtime nbs -1
    execute store result storage nbs:main playing.index int 1 run scoreboard players get songindex nbs
    function ~/../change_song2 with storage nbs:main playing

function ~/change_song2:
    $data modify storage nbs:main playing set from storage nbs:main songs[$(index)]


#> Controls

function ~/lever:
    function ~/../place_lever
    trigger next

function ~/countdown:
    scoreboard players set countdown nbs 5
    scoreboard players set songindex nbs 0
    scoreboard players set playing nbs 1
    title @a clear
    title @a times 0 0 1s
    function ~/../change_song

function ~/launch:
    scoreboard players set songindex nbs 1
    scoreboard players set playing nbs 1
    schedule function ~/../launch_particle f"210t" replace
    schedule function ~/../launch_title f"340t" replace
    function ~/../launch_sign
    function ~/../change_song

function ~/cleanup:
    kill @e[type=item_display,tag=nbs_launch_sign]
    playsound minecraft:entity.item.pickup record @a 0 96 -146 8 1
    particle cloud 0 96 -144 0.25 0.25 0.25 0.1 10 force
    function ~/../
    function ~/../destroy_lever


#> Effects

function ~/place_lever:
    fill 0 77 -141 0 78 -141 minecraft:bricks destroy
    fill 0 77 -141 0 78 -141 minecraft:bricks destroy
    setblock 0 78 -140 minecraft:lever[facing=south]

function ~/destroy_lever:
    fill 0 77 -141 0 78 -141 minecraft:air destroy

function ~/lever_interaction:
    playsound minecraft:block.portal.trigger record @a ~ ~ ~ 1 2.0
    particle portal 0 78 -140 0 0 0 3 1000
    execute if block 0 78 -140 minecraft:lever[powered=true] run schedule function ~/../launch 40t replace

speakers = [
    (-21, 91, -134),
    (-24, 91, -128),
    (-24, 91, -120),
    (-24, 91, -111),
    (-24, 91, -102),
]

function ~/speaker_sweep:
    for side in [1, -1]:
        for speaker in speakers:
            particle minecraft:sweep_attack f"{speaker[0] * side}" f"{speaker[1]}" f"{speaker[2]}" ~ ~ ~ 1 0 force

function ~/speaker_explode:
    for side in [1, -1]:
        for speaker in speakers:
            particle minecraft:explosion f"{speaker[0] * side}" f"{speaker[1]}" f"{speaker[2]}" ~ ~ ~ 1 0 force

function ~/event/countdown:
    effect give @a minecraft:blindness 1 1 true
    title @a title {"score":{"name":"countdown","objective":"nbs"}}
    function ~/../../speaker_explode
    scoreboard players remove countdown nbs 1

function ~/event/melody:
    effect clear @a[x=-27,y=74,z=-135,dx=54,dy=8,dz=20,tag=nbs_glowing] minecraft:glowing
    tag @a[tag=nbs_glowing] remove nbs_glowing
    tag @r[x=-27,y=74,z=-135,dx=54,dy=8,dz=20] add nbs_glowing
    effect give @a[x=-27,y=74,z=-135,dx=54,dy=8,dz=20,tag=nbs_glowing] minecraft:glowing 1 1 true

function ~/event/counter:
    function ~/../../launch_fireworks_small

function ~/event/roll:
    function ~/../../speaker_sweep

function ~/event/end:
    summon lightning_bolt 0 95 -145
    for i in range(5):
        schedule function ~/../../launch_fireworks_large f"{i}t" append
    for i in range(10):
        function ~/../../launch_fireworks_large
    function ~/../../speaker_explode

function ~/beat:
    if score songindex nbs matches 1 run function ~/../launch_tick

function ~/launch_tick:
    particle note 0 85 -128 10 10 10 1 25

function ~/launch_particle:
    particle portal 0 96 -149 0 0 0 4 1000
    particle enchant 0 96 -149 0 0 0 5 1000 force

function ~/launch_title:
    trigger next
    title @a reset
    title @a title ["",{"text":"Note ","color":"blue","bold":true},{"text":"Block ","color":"dark_green","bold":true},{"text":"World","color":"dark_aqua","bold":true}]
    title @a subtitle ["",{"text":"Now LIVE at ","color":"white"},{"text":"noteblock.world","underlined":true,"color":"aqua"},{"text":"!","color":"white"}]
    playsound minecraft:entity.player.levelup record @a

function ~/launch_fireworks_small:
    execute store result storage nbs:main coords.x int 1 run random value -22..22
    execute store result storage nbs:main coords.y int 1 run random value 80..94
    execute store result storage nbs:main coords.z int 1 run random value -150..-138
    function ~/../launch_fireworks_small2 with storage nbs:main coords

function ~/launch_fireworks_large:
    execute store result storage nbs:main coords.x int 1 run random value -20..20
    execute store result storage nbs:main coords.y int 1 run random value 82..92
    execute store result storage nbs:main coords.z int 1 run random value -145..-140
    function ~/../launch_fireworks_large2 with storage nbs:main coords

function ~/launch_fireworks_small2:
    $summon firework_rocket $(x) $(y) $(z) {LifeTime:0,FireworksItem:{id:firework_rocket,count:1,components:{fireworks:{flight_duration:1,explosions:[{shape:"small_ball",has_twinkle:0,has_trail:0,colors:[I;15790320]}]}}}}

function ~/launch_fireworks_large2:
    $summon firework_rocket $(x) $(y) $(z) {LifeTime:0,FireworksItem:{id:firework_rocket,count:1,components:{fireworks:{flight_duration:1,explosions:[{shape:"large_ball",has_twinkle:1,has_trail:1,colors:[I;2651799,4312372,6719955]}]}}}}

function ~/summon_sign:
    kill @e[type=item_display,tag=nbs_launch_sign]
    summon minecraft:item_display 0.5 112.0 -148.375 {Tags:["nbs_launch_sign", "nbw_cube"], item: {components: {"minecraft:custom_model_data": 48185, "minecraft:custom_name": '{"color":"yellow","italic":false,"text":"nbs:nbw_block"}'}, count: 1, id: "minecraft:note_block"}, transformation: {left_rotation: [0.21294273f, 0.77828604f, 0.5052195f, 0.30607137f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [5.375001f, 5.374359f, 5.375002f], translation: [0.0f, 0.0f, 0.0f]}}
    summon minecraft:item_display 0.5 109.0 -145.6875 {Tags:["nbs_launch_sign", "nbw_text"], item: {components: {"minecraft:custom_model_data": 48186, "minecraft:custom_name": '{"color":"yellow","italic":false,"text":"nbs:nbw_text"}'}, count: 1, id: "minecraft:note_block"}, transformation: {left_rotation: [-0.090632565f, 0.0f, 0.0f, 0.9958844f], right_rotation: [0.0f, 0.0f, 0.0f, 1.0f], scale: [6.062499f, 6.0624943f, 6.0624976f], translation: [-0.3499448f, 0.04870289f, -0.3465393f]}, view_range: 0.657277f}

function ~/launch_sign:
    positioned 0 109 -148 as @e[distance=..5,type=item_display,tag=nbs_launch_sign] run data merge entity @s {
        transformation: {
            translation: [0.0f, -15.0f, 0.0f],
        },
        start_interpolation: 0,
        interpolation_duration: 255,
    }